apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'bintray'
apply plugin: 'idea'

targetCompatibility = JavaVersion.VERSION_1_6
sourceCompatibility = JavaVersion.VERSION_1_6

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile project(':processor')

  compileOnly "com.android.tools.build:gradle-api:$androidToolsVersion"
}

def generatedDir = new File(projectDir, "generated")
def generatedJavaSourcesDir = new File(generatedDir, "main/java")

task generateBuildClass {
  inputs.property('version', version)
  outputs.dir generatedDir

  doLast {
    def buildClassFile = new File(generatedJavaSourcesDir, "io/michaelrocks/lightsaber/plugin/Build.java")
    buildClassFile.parentFile.mkdirs()
    buildClassFile.text = "" +
        "package io.michaelrocks.lightsaber.plugin;\n" +
        "\n" +
        "public class Build {\n" +
        "    public static final String VERSION = \"$version\";\n" +
        "}\n"
  }
}

sourceSets {
  main {
    output.dir(builtBy: tasks.generateBuildClass, generatedJavaSourcesDir)
    groovy.srcDirs += generatedJavaSourcesDir
  }
}

tasks.withType(GroovyCompile) {
  it.dependsOn tasks.generateBuildClass
}

clean.doFirst {
  delete generatedDir
}

idea {
  module {
    generatedSourceDirs += generatedJavaSourcesDir
  }
}
